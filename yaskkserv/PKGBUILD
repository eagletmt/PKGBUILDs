# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=yaskkserv
pkgver=0.5.4
pkgrel=2
pkgdesc='Yet another SKK Japanese input method dictionary server'
arch=('x86_64' 'i686')
url='http://umiushi.org/~wac/yaskkserv/'
license=('GPL')
depends=('gcc-libs')
makedepends=('perl' 'skk-jisyo')
backup=('etc/rc.d/yaskkserv' 'etc/conf.d/yaskkserv')
source=(http://umiushi.org/~wac/$pkgname/$pkgname-$pkgver.tar.bz2
        yaskkserv.conf
        yaskkserv.service)
install=yaskkserv.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LC_ALL=C ./configure --prefix=/usr --enable-google-japanese-input
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install_all
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"

  mkdir -p "$pkgdir/usr/share/skk"
  "$pkgdir/usr/bin/yaskkserv_make_dictionary" /usr/share/skk/SKK-JISYO.L "$pkgdir/usr/share/skk/SKK-JISYO.L.yaskkserv"

  mkdir -p "$pkgdir/etc/conf.d"
  install -m644 "$srcdir/yaskkserv.conf" "$pkgdir/etc/conf.d/yaskkserv"

  local _systemd_dir="$pkgdir/usr/lib/systemd"
  mkdir -p "$_systemd_dir/system"
  install -m644 "$srcdir/yaskkserv.service" "$_systemd_dir/system/"
}

# vim:set ts=2 sw=2 et:
md5sums=('7cd86e0827d53214f15dcac8efec3d5f'
         'b7e58289efc5cb5952c51028303de77b'
         '2891b69c4f0c80525c1b01358d3cbd5b')
