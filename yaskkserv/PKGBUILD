# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=yaskkserv
pkgver=0.5.2
pkgrel=1
pkgdesc='Yet another SKK Japanese input method dictionary server'
arch=('x86_64' 'i686')
url='http://umiushi.org/~wac/yaskkserv/'
license=('GPL')
depends=('gcc-libs')
makedepends=('perl' 'skk-jisyo')
backup=('etc/rc.d/yaskkserv')
source=(http://umiushi.org/~wac/$pkgname/$pkgname-$pkgver.tar.bz2
        yaskkserv)
install=yaskkserv.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LC_ALL=C ./configure --prefix=/usr --enable-google-japanese-input
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install_all

  mkdir -p "$pkgdir/usr/share/skk"
  "$pkgdir/usr/bin/yaskkserv_make_dictionary" /usr/share/skk/SKK-JISYO.L "$pkgdir/usr/share/skk/SKK-JISYO.L.yaskkserv"

  mkdir -p "$pkgdir/etc/rc.d"
  install -m755 "$srcdir/yaskkserv" "$pkgdir/etc/rc.d/"
}

# vim:set ts=2 sw=2 et:
md5sums=('6dfd666624ceaa124c4f502187c5721b'
         '6633199b07afbbf19c7c0eca618e81b5')
