# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=yaskkserv
pkgver=0.4.0
pkgrel=1
pkgdesc='Yet another SKK Japanese input method dictionary server'
arch=('x86_64' 'i686')
url='http://umiushi.org/~wac/yaskkserv/'
license=('GPL')
depends=('gcc-libs')
makedepends=('skk-jisyo')
backup=('etc/rc.d/yaskkserv')
source=(http://umiushi.org/~wac/$pkgname/$pkgname-$pkgver.tar.bz2
        yaskkserv)
install=yaskkserv.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LC_ALL=C ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install_all

  mkdir -p "$pkgdir/usr/share/skk"
  "$pkgdir/usr/bin/yaskkserv_make_dictionary" /usr/share/skk/SKK-JISYO.L "$pkgdir/usr/share/skk/SKK-JISYO.L.yaskkserv"

  mkdir -p "$pkgdir/etc/rc.d"
  install -m755 "$srcdir/yaskkserv" "$pkgdir/etc/rc.d/"
}

md5sums=('83e1871f06a6002e492a431a16810fcd'
         '6cd7b1a9cfefb0de44194cfff9a0a6ca')
