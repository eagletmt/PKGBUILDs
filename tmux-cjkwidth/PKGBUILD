# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=tmux-cjkwidth
pkgver=2.0
pkgrel=1
pkgdesc='A terminal multiplexer'
url='http://tmux.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent' 'libutempter')
source=(http://downloads.sourceforge.net/tmux/tmux-${pkgver/_/}.tar.gz
        LICENSE
        wcwidth.patch)
conflicts=('tmux')
provides=("tmux=$pkgver-$pkgrel")

prepare() {
  cd "$srcdir/tmux-${pkgver/_/}"
  patch -Np1 -i "$srcdir/wcwidth.patch"
}

build() {
  cd "$srcdir/tmux-${pkgver/_/}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/tmux-${pkgver/_/}"
  make install DESTDIR=$pkgdir
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"

  install -dm755 "$pkgdir/usr/share/tmux/"
  install -m644 examples/* "$pkgdir/usr/share/tmux/"
  install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"

  install -d $pkgdir/usr/share/bash-completion/completions/
  mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/usr/share/bash-completion/completions/tmux
}

sha1sums=('977871e7433fe054928d86477382bd5f6794dc3d'
          '85c4a73c14caa54b11777d97964ac7bc2a8101f5'
          'df4f48beff86bdbce39d862c34f22999208fe42e')
