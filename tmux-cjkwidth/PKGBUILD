# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=tmux-cjkwidth
pkgver=1.5
pkgrel=1
pkgdesc="A terminal multiplexer, cjkwidth patched"
url="http://tmux.sourceforge.net/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses' 'libevent')
source=(http://downloads.sourceforge.net/tmux/tmux-$pkgver.tar.gz
	LICENSE
  cjkwidth.patch)
md5sums=('3d4b683572af34e83bc8b183a8285263'
         '71601bc37fa44e4395580b321963018e'
         '5618aa6847d04eab0ae31189b89e1dd5')
conflicts=('tmux')
provides=("tmux=$pkgver-$pkgrel")

build() {
  cd "$srcdir/tmux-$pkgver"
  patch -Np0 -i "$srcdir/cjkwidth.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/tmux-$pkgver"
  make install DESTDIR=$pkgdir
  install -Dm644 examples/tmux.vim "$pkgdir/usr/share/vim/vimfiles/syntax/tmux.vim"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/tmux/LICENSE"
  install -dm755 "$pkgdir/usr/share/tmux/"
  install -m644 examples/* "$pkgdir/usr/share/tmux/"
#  mv $pkgdir/usr/man $pkgdir/usr/share/
  mkdir -p $pkgdir/etc/bash_completion.d/
  mv $pkgdir/usr/share/tmux/bash_completion_tmux.sh $pkgdir/etc/bash_completion.d/tmux
}
