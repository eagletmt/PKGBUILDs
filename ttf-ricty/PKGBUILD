# Maintainer: eagletmt <eagletmt@gmail.com>
pkgname=ttf-ricty
pkgver=3.2.0
pkgrel=1
pkgdesc='Japanese TrueType font for developers by mixing Inconsolata and Migu 1M.'
arch=('any')
url='http://save.sys.t.u-tokyo.ac.jp/~yusa/fonts/ricty.html'
license=('public domain')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('fontforge' 'ttf-inconsolata' 'ttf-migu')
install=ttf.install
source=(Ricty-$pkgver.tar.gz::https://github.com/yascentur/Ricty/tarball/$pkgver)
_commit=b9d8b9c

build() {
  cd "$srcdir/yascentur-Ricty-$_commit"
  ./ricty_generator.sh /usr/share/fonts/TTF/{ttf-inconsolata.otf,migu-1m-regular.ttf,migu-1m-bold.ttf}
}

package() {
  cd "$srcdir/yascentur-Ricty-$_commit"
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF/"
}

md5sums=('780d3ce63869475ddf723456e45b9710')
