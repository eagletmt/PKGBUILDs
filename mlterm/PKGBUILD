# Maintainer: Yoel Lion <yoel3ster at gmail dot com>
# Last Maintainer: Netanel Shine <Netanelshine@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Gatekeeper <cool_work@yahoo.com>

pkgname=mlterm
pkgver=3.0.11
pkgrel=1
pkgdesc="Multi Lingual TERMinal emulator on X"
url="http://mlterm.sourceforge.net/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('fribidi' 'gtk2' 'libsm')
makedepends=('intltool' 'librsvg' 'ncurses')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz ${pkgname}.desktop)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # build
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/mlterm --sysconfdir=/etc --mandir=/usr/share/man \
    --enable-fribidi --enable-optimize-redrawing --enable-utmp \
    --enable-m17nlib --disable-static \
    --with-tools=mlconfig,mlterm-zoom,mlterm-menu,mlclient \
    --with-imagelib=gdk-pixbuf --with-type-engines=xcore,xft
  make
  make DESTDIR=${pkgdir} install
  echo 'conf_menu_path_2 = mlterm-zoom' >> ${pkgdir}/etc/mlterm/main

  # install some freedesktop.org compatibility
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
  sed -e 's:mlterm:mlclient:' \
   < ${pkgdir}/usr/share/applications/${pkgname}.desktop \
   > ${pkgdir}/usr/share/applications/mlclient.desktop
  install -D -m644 contrib/icon/${pkgname}-icon.svg \
    ${pkgdir}/usr/share/pixmaps/${pkgname}.svg
  install -D -m644 contrib/icon/${pkgname}-icon-trans.svg \
    ${pkgdir}/usr/share/pixmaps/mlclient.svg

  # finally we render a png as fallback for not svg aware menu applications
  # Attention: always make sure you check the dimensions of the source-svg,
  # you can read the dimensions via inkscapes export funktion
  cd ${pkgdir}/usr/share/pixmaps
  rsvg-convert -w 48 -h 38 -f png -o mlterm.png mlterm.svg
  rsvg-convert -w 48 -h 38 -f png -o mlclient.png mlclient.svg
}

md5sums=('39c1236433f9a03c865a86b4e8ff7170'
         'cea39a3e312d0a40a1e839d79bec03ed')
