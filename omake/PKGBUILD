# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Mildred <silkensedai@online.fr>

pkgname=omake
pkgver=0.9.8.6_0.rc1
pkgrel=2
pkgdesc="A build system designed for scalability and portability"
arch=('i686' 'x86_64')
url="http://omake.metaprl.org/index.html"
license=('GPL')
depends=('readline' 'gamin')
makedepends=('make' 'ocaml' 'patch')
source=(http://omake.metaprl.org/downloads/$pkgname-${pkgver/_/-}.tar.gz \
        disable-ocaml3.12-warnings.patch)

build() {
  cd "$srcdir/$pkgname-${pkgver%_*}"

  patch -Np0 -i "$srcdir/disable-ocaml3.12-warnings.patch"

  make bootstrap
  make PREFIX=/usr all
}

package() {
  cd "$srcdir/$pkgname-${pkgver%_*}"
  make PREFIX=/usr INSTALL_ROOT="$pkgdir" install
}

md5sums=('fe39a476ef4e33b7ba2ca77a6bcaded2'
         '6dc6b4f08f3b8af0ba320a7ccc94e606')
