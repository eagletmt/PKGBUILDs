# Maintainer: Kohei Suzuki <eagletmt@gmail.com>
pkgname=mackerel-agent
pkgver=0.13.0
pkgrel=1
pkgdesc="A revolutionary new kind of application performance management"
arch=('i686' 'x86_64')
url="https://mackerel.io/"
license=('Apache')
depends=()
makedepends=('go' 'git')
backup=('etc/mackerel-agent/mackerel-agent.conf')
source=(https://github.com/mackerelio/$pkgname/archive/v$pkgver.tar.gz
        mackerel-agent.conf
        mackerel-agent.service)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export GOPATH="$srcdir/go"
  mkdir "$GOPATH"
  go get -d github.com/mackerelio/mackerel-agent
  go build \
    -ldflags="\
    -X github.com/mackerelio/mackerel-agent/version.VERSION $pkgver" \
    -o build/$pkgname \
    github.com/mackerelio/$pkgname
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  # Disable test because it requires "eth0" interface.
  # make test
}

package() {
  install -Dm755 "$srcdir/$pkgname-$pkgver/build/mackerel-agent" "$pkgdir/usr/bin/mackerel-agent"
  install -Dm644 "$srcdir/mackerel-agent.conf" "$pkgdir/etc/mackerel-agent/mackerel-agent.conf"
  install -Dm644 "$srcdir/mackerel-agent.service" "$pkgdir/usr/lib/systemd/system/mackerel-agent.service"

  mkdir -p "$pkgdir/var/lib/mackerel-agent"
}

sha1sums=('248fbed00d94d7cd29c86750565e4077ff91c7ff'
          '16fd84221d4b72120b881a13b70d7b8ce79f3817'
          'f59f30f5b8ea186a4b8ffd9e3986623cb2300630')
