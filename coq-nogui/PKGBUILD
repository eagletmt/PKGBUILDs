# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq-nogui
pkgver=8.4.pl1
_pkgver=8.4pl1
pkgrel=1
pkgdesc='Formal proof management system, without CoqIDE'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('ocaml')
makedepends=('camlp5-transitional' 'hevea')
source=("http://coq.inria.fr/distrib/V$_pkgver/files/coq-$_pkgver.tar.gz")
provides=("coq=$pkgver")
conflicts=('coq')

build() {
  cd "coq-$_pkgver"

  ./configure \
    -prefix /usr \
    -mandir /usr/share/man \
    -configdir /etc/xdg/coq \
    -opt \
    -coqide no \
    -with-doc no

  make -j1 world

  make doc-html
}

package() {
  cd "coq-$_pkgver"

  make COQINSTALLPREFIX="$pkgdir" install

  make COQINSTALLPREFIX="$pkgdir" install-doc-html
}

md5sums=('07e44e89fc99d6c414605dc96be37f12')
